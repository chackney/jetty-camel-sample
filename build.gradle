apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'application'
apply plugin: 'pmd'
apply plugin: 'checkstyle'


// Properties for module - create the properties needed
group = 'org.muckingabout'

version = '0.1-SNAPSHOT'

description = """Test Project For Mucking about"""

sourceCompatibility = 1.7
targetCompatibility = 1.7

mainClassName = "org.muckingabout.Application"

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    compile.exclude(module: 'mockito-all')
}

dependencies {
    compile group: 'org.apache.camel', name: 'camel', version: '2.15.0'
    compile group: 'org.apache.camel', name: 'camel-servlet', version: '2.15.0'
    compile group: 'org.apache.camel', name: 'camel-jetty9', version: '2.15.0'
    compile group: 'org.apache.camel', name: 'camel-netty-http', version: '2.15.0'
    compile group: 'org.apache.camel', name: 'camel-swagger', version: '2.15.0'
    compile group: 'org.apache.camel', name: 'camel-jackson', version: '2.15.0'
    compile group: 'org.apache.camel', name: 'camel-test', version: '2.15.0'
    compile group: 'com.netflix.hystrix', name: 'hystrix-core', version:'1.3.16'
    compile group: 'com.netflix.hystrix', name: 'hystrix-metrics-event-stream', version:'1.3.16'
    compile group: 'org.slf4j', name: 'slf4j-simple', version:'1.7.12'
    compile group: 'com.jayway.restassured', name: 'rest-assured', version:'2.3.4'
}

jar {
    manifest {
        attributes "Main-Class": "$mainClassName"
    }
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': "org.muckingabout.Application"
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

// Override/Custom tasks
test {
    useJUnit {
        includeCategories 'org.muckingabout.testClasses.AcceptanceTest'
        excludeCategories 'org.muckingabout.testClasses.IntegrationTest'
    }
}

task integrationTests(type: Test) {
    useJUnit {
        includeCategories 'org.muckingabout.testClasses.IntegrationTest'
    }
}

